## [H-01] Malicious validator can steal user funds by front-running withdrawal credentials

Co-reporter [Aghasi](https://twitter.com/M3talDrag0n)

### Description

PATH: `Staking.sol:initiateValidatorsWithDeposits` L396-457

By default in this protocol, the ones who have `INITIATOR_SERVICE_ROLE` and `STAKING_MANAGER_ROLE` manage the beacon chain validator creation process. The validator would sign the deposit data with the withdrawal credentials set to a protocol-controlled contract so that withdrawals would be received by the protocol (`withdrawalWallet_`).

In case the validator is malicious, they can monitor the mempool and front-run the `deposit` transaction of the beacon chain for its pubKey and deposit 1 ether for different withdrawal credentials. Because of the beacon chain implementation and ETH specs ( link ), in `ProcessDeposit`, if the `pubKey` is already registered, it increases its balance, not touching the `withdrawal_credentials`. 

When the deposit reaches the consensus layer, the validator would be granted the 32 ETH of user funds and they can withdraw this to their own address.

This bug was also present in Lido and Frax:

https://research.lido.fi/t/mitigations-for-deposit-front-running-vulnerability/1239

https://github.com/code-423n4/2022-09-frax-findings/issues/81

### Code Snippet

```solidity
function initiateValidatorsWithDeposits(ValidatorParams[] calldata validators)
        external
        onlyRole(INITIATOR_SERVICE_ROLE)
    {
        if (pauser.isInitiateValidatorsPaused()) {
            revert Paused();
        }
        if (validators.length == 0) {
            return;
        }

        // First loop is to check that all validators are valid according to our constraints and we record the
        // validators and how much we have deposited.
        uint256 amountDeposited = 0;
        for (uint256 i = 0; i < validators.length; ++i) {
            ValidatorParams calldata validator = validators[i];

            if (usedValidators[validator.pubkey]) {
                revert PreviouslyUsedValidator();
            }

            if (validator.depositAmount < minimumDepositAmount) {
                revert MinimumValidatorDepositNotSatisfied();
            }

            if (validator.depositAmount > maximumDepositAmount) {
                revert MaximumValidatorDepositExceeded();
            }

            if (validator.depositAmount > allocatedETHForDeposits) {
                revert NotEnoughDepositETH();
            }

            _requireProtocolWithdrawalAccount(validator.withdrawalCredentials);

            usedValidators[validator.pubkey] = true;
            amountDeposited += validator.depositAmount;

            emit ValidatorInitiated({
                id: keccak256(validator.pubkey),
                operatorID: validator.operatorID,
                pubkey: validator.pubkey,
                amountDeposited: validator.depositAmount
            });
        }

        allocatedETHForDeposits -= amountDeposited;
        totalDepositedInValidators += amountDeposited;
        numInitiatedValidators += validators.length;

        // Second loop is to send the deposits to the deposit contract. Keeps external calls to the deposit contract
        // separate from state changes.
        for (uint256 i = 0; i < validators.length; ++i) {
            ValidatorParams calldata validator = validators[i];
            depositContract.deposit{value: validator.depositAmount}({
                pubkey: validator.pubkey,
                withdrawal_credentials: validator.withdrawalCredentials,
                signature: validator.signature,
                deposit_data_root: validator.depositDataRoot
            });
        }
    }
```

### Remediation

This issue can be mitigated by either having validators pre-deposit 1 ETH with the protocolâ€™s withdrawal credentials and check this on-chain before adding it to Operator Registry.

Another mitigation (similar to Lido) is having the caller of deposit also supply the latest deposit state root from the DepositContract. This should then be checked before making the deposit. This ensures that there were no calls to the deposit contract between the signing and the actual depositing.


## [M-01] Deposit share rate can be manipulated by staking manager

### Description

PATH: `Staking.sol:setExchangeAdjustmentRate` (L599-610)

In the current implementation of the `setExchangeAdjustmentRate` function, there is a check for `exchangeAdjustmentRate_` value to be less or equal to the `_BASIS_POINTS_DENOMINATOR` (10,000). By setting `exchangeAdjustmentRate_` equal to `_BASIS_POINTS_DENOMINATOR`, the function `ethToMntETH` would always return a 0 value. This way users always will get a 0 amount of `mntETH` for staking.

The staking functionality also does not have any slippage protection and as such it leads to centralisation risk where the staking manager can deny people shares or steal ETH by front-running large deposits.

### Code Snippet

```solidity
function setExchangeAdjustmentRate(uint16 exchangeAdjustmentRate_) external onlyRole(STAKING_MANAGER_ROLE) {
        if (exchangeAdjustmentRate_ > _BASIS_POINTS_DENOMINATOR) {
            revert InvalidConfiguration();
        }

        exchangeAdjustmentRate = exchangeAdjustmentRate_;
        emit ProtocolConfigChanged(
            this.setExchangeAdjustmentRate.selector,
            "setExchangeAdjustmentRate(uint16)",
            abi.encode(exchangeAdjustmentRate_)
        );
    }
```

```solidity
function ethToMntETH(uint256 ethAmount) public view returns (uint256) {
        // 1:1 if there is no controlled ETH.
        if (totalControlled() == 0) {
            return ethAmount;
        }

        // deltaMntETH = (1 - exchangeAdjustmentRate) * (mntEthSupply / totalControlled) * ethAmount
        // This rounds down to zero in the case of `(1 - exchangeAdjustmentRate) * ethAmount * mntEthSupply <
        // totalControlled`.
        // While this scenario is theoretically possible, it can only be realised feasibly during the protocol's
        // bootstrap phase and if `totalControlled` and `mntEthSupply` can be changed independently of each other. Since
        // the former is permissioned, and the latter is not permitted by the protocol, this cannot be exploited by an
        // attacker.
        return Math.mulDiv(
            ethAmount,
            mntETH.totalSupply() * uint256(_BASIS_POINTS_DENOMINATOR - exchangeAdjustmentRate),
            totalControlled() * uint256(_BASIS_POINTS_DENOMINATOR)
        );
    }
```

### Remediation

To prevent such a situation, adjusting the validation check in the `setExchangeAdjustmentRate` function is recommended at least to disallow the `exchangeAdjustmentRate_` from being a too large value (e.g. a maximum of 10% of `_BASIS_POINTS_DENOMINATOR`).

## [M-02] Inconsistent Minimum Deposit Check for Assets and Shares

### Description

`MevEth.sol:mint(uint256,address)` (L543-560)

When depositing, the contract checks that the assets should be greater than `MIN_DEPOSIT`. However, when minting, it checks that the shares should be greater than `MIN_DEPOSIT`. 

`fraction.elastic` will keep consistently increasing through the payment of rewards and this will cause a growing imbalance between `fraction.elastic` and `fraction.base` over time. This would cause a discrepancy between the minimum assets amount for deposit and mint and that lacks logical consistency and equality for users. 

It would be more reasonable and fair for both depositing and minting operations to have equal checks to ensure consistency in the contract's behavior and user experience.

### Code Snippet

```solidity
function deposit(uint256 assets, address receiver) external payable stakingUnpaused returns (uint256 shares) {
        // If the deposit is less than the minimum deposit, revert
        if (assets < MIN_DEPOSIT) revert MevEthErrors.DepositTooSmall();

        // Convert the assets to shares and update the fraction elastic and base
        shares = convertToShares(assets);

        fraction.elastic += uint128(assets);
        fraction.base += uint128(shares);

        // Deposit the assets
        _deposit(assets);

        // Mint MevEth shares to the receiver
        _mint(receiver, shares);

        // Emit the deposit event to notify offchain listeners that a deposit has occured
        emit Deposit(msg.sender, receiver, assets, shares);
    }
```
```solidity
function mint(uint256 shares, address receiver) external payable stakingUnpaused returns (uint256 assets) {
        // If the deposit is less than the minimum deposit, revert
        if (shares < MIN_DEPOSIT) revert MevEthErrors.DepositTooSmall();

        // Convert the shares to assets and update the fraction elastic and base
        assets = convertToAssets(shares);

        fraction.elastic += uint128(assets);
        fraction.base += uint128(shares);

        // Deposit the assets
        _deposit(assets);
        // Mint MevEth shares to the receiver
        _mint(receiver, shares);

        // Emit the deposit event to notify offchain listeners that a deposit has occured
        emit Deposit(msg.sender, receiver, assets, shares);
    }
```

### Remediation

Function `mint` can be rewritten like this:

```solidity
    function mint(uint256 shares, address receiver) external payable stakingUnpaused returns (uint256 assets) {

        // Convert the shares to assets and update the fraction elastic and base
        assets = convertToAssets(shares);
        
        // If the deposit is less than the minimum deposit, revert
        if (assets < MIN_DEPOSIT) revert MevEthErrors.DepositTooSmall();
    
    [...]
```

## Full Report

Full audit report can be found [here](https://github.com/Hexens/Smart-Contract-Review-Public-Reports/blob/main/Mantle_SCs_Aug23(Public)(Liquid%20Staking%20Protocol).pdf).
